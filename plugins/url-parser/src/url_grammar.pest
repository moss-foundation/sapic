// FIXME: Most literal|var should be refined to allow mixing of literal and variable
// For example: {{env}}-test.sapic.com

// host.com
// host.com#fragment
// host.com?query
// host.com?query=
// host.com?query=1
// host.com?query=1#fragment
// host.com/
// host.com/#fragment
// host.com/path
// host.com/path#fragment
// host.com/path?query=1
// host.com/path?query=1#fragment

url = {
    SOI ~
    scheme_part? ~
    host_part ~
    // Ensure we can handle trailing slash immediately after the host part without a path part
    (path_part|"/"?) ~
    query_part? ~
    fragment_part? ~
    EOI
    }
var = { "{{" ~ ident ~ "}}" }
ident = @{ (ASCII_ALPHANUMERIC | "_" )+ }

scheme_part = { (var | scheme_literal) ~ "://" }
scheme_literal = @{
    ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "+" | "-" | ".")*
}

// HOST
// host_part must stop before '/', '?' or '#'. Inside host_part we only split on var boundaries.
// host_raw will not consume '{' sequences or any of the host terminators.
host_part  = { (var | host_raw)+ }
host_raw   = @{ (!"{{" ~ !"/" ~ !"?" ~ !"#" ~ (ASCII_ALPHANUMERIC | "-" | "."))+ }


// Allow trailing slash
path_part = { ("/" ~ path_segment)+ ~ "/"? }
path_var = { ":" ~ ident }
path_segment = { path_var | path_text }
path_text = @{
    (!("/" | "?" | "#" | "{") ~ ANY)+
}

query_part = { "?" ~ query_param ~ ("&" ~ query_param)* }
query_param = { query_key ~ ("=" ~ query_value)? }
query_key = @{
    (ASCII_ALPHANUMERIC | "_" | "-" )+
}
// Allow missing value like ?q=, where the value is interpreted as an empty string
query_value = { (var | query_text)* }
query_text = @{
    (!("&" | "#" | "{") ~ ANY)+
}

fragment_part = { "#" ~ (var | fragment_text)+ }
fragment_text = @{
    (!"{" ~ ANY)+
}

// Variable rule -> parser used by either url or body parser
// Find out if it supports import?